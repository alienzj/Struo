rule prodigal:
    """
    Running prodigal on each genome
    """
    input:
        fasta = lambda wildcards: config['samples'].loc[wildcards.sample, config['fasta_file_path_col']]
    output:
        fna = temp(config['tmp_dir'] + 'prodigal/{sample}.fna'),
        faa = temp(config['tmp_dir'] + 'prodigal/{sample}.faa'),
        gbk = temp(config['tmp_dir'] + 'prodigal/{sample}.gbk')
    params:
        params = config['params']['prodigal']
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt * 2 * 8
    conda:
        '../envs/humann2.yaml'
    log:
        log_dir + 'prodigal/{sample}.log'
    benchmark:
        benchmark_dir + 'prodigal/{sample}.txt'
    shell:
        """
        gunzip -c {input.fasta} | \
          prodigal {params.params} \
          -o {output.gbk} -d {output.fna} -a {output.faa} \
          2>&1 > {log}
        """

rule diamond:
    """
    Annotating genes via diamond search of UniRef db
    """
    input:
        faa = config['tmp_dir'] + 'prodigal/{sample}.faa',
	dmnd_db = config['params']['diamond_db']
    output:
        hits = temp(config['tmp_dir'] + 'diamond/{sample}_hits.txt')
    params:
        params = config['params']['diamond'],
        tmp_dir = config['tmp_dir']
    threads:
        8
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 12,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 2
    conda:
        '../envs/humann2.yaml'
    log:
        log_dir + 'annotate_genes/{sample}.log'
    benchmark:
        benchmark_dir + 'annotate_genes/{sample}.txt'
    shell:
        """
        diamond blastp {params.params} \
           -q {input.faa} -d {input.dmnd_db} -o {output.hits} \
           --tmpdir {params.tmp_dir} --threads {threads} \
           --outfmt 6 qseqid sseqid pident length qstart qend qlen sstart send slen evalue \
           2>&1 > {log}
        """

rule annotate_genes:
    """
    Annotating genes via diamond search of UniRef db
    """
    input:
        hits = config['tmp_dir'] + 'diamond/{sample}_hits.txt',
        fna = config['tmp_dir'] + 'prodigal/{sample}.fna',
        faa = config['tmp_dir'] + 'prodigal/{sample}.faa',
    output:
        fna = temp(config['tmp_dir'] + 'prodigal/{sample}_annot.fna'),
        faa = temp(config['tmp_dir'] + 'prodigal/{sample}_annot.faa')
    params:
        tax = lambda wildcards: config['samples'].loc[wildcards.sample, config['taxonomy_col']],
        taxID = lambda wildcards: config['samples'].loc[wildcards.sample, config['taxID_col']],
        exe = config['pipeline']['script_folder'] + 'annotate_genes.py',
	prefix = config['tmp_dir'] + 'prodigal/{sample}'
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 14,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 6
    conda:
        '../envs/humann2.yaml'
    log:
        log_dir + 'annotate_genes/{sample}.log'
    benchmark:
        benchmark_dir + 'annotate_genes/{sample}.txt'
    shell:
        """
        {params.exe} --prefix {params.prefix} \
           --columns qseqid,sseqid,pident,length,qstart,qend,qlen,sstart,send,slen,evalue \
           {input.hits} {input.fna} {input.faa} "{params.tax}" {params.taxID} \
           > {log} 2>&1
        """

rule cluster_genes_nuc:
    """
    Clustering genes and taking the centroid
    """
    input:
        config['tmp_dir'] + 'prodigal/{sample}_annot.fna'
    output:
        temp(config['tmp_dir'] + 'vsearch/{sample}_annot_reps.fna')
    params:
        params = config['params']['vsearch']
    threads:
        8
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt * 2
    conda:
        '../envs/humann2.yaml'
    log:
        log_dir + 'cluster_genes_nuc/{sample}.log'
    benchmark:
        benchmark_dir + 'cluster_genes_nuc/{sample}.txt'
    shell:
        """
         vsearch {params.params} \
           --threads {threads} \
           --cluster_fast {input} \
           --centroids {output} \
           2>&1 > {log}
        """

rule get_rep_genes_AA:
    """
    Selecting the same genes as present in nuc file
    """
    input:
        fna = config['tmp_dir'] + 'vsearch/{sample}_annot_reps.fna',
        faa = config['tmp_dir'] + 'prodigal/{sample}_annot.faa'
    output:
        faa = temp(config['tmp_dir'] + 'vsearch/{sample}_annot_reps.faa')
    params:
        exe = config['pipeline']['script_folder'] + 'filter_seqs.py'
    resources:
        time = lambda wildcards, attempt: attempt ** 2 * 59,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 2
    log:
        log_dir + 'get_rep_genes_AA/{sample}.log'
    benchmark:
        benchmark_dir + 'get_rep_genes_AA/{sample}.txt'
    shell:
        """
        {params.exe} {input.faa} {input.fna} > {output.faa} 2> {log}
        """

localrules: cat_genes_nuc

rule cat_genes_nuc:
    """
    Combining genes
    """
    input:
        expand(config['tmp_dir'] + 'vsearch/{sample}_annot_reps.fna',
	      sample = config['samples_unique'])
    output:
        config['tmp_dir'] + 'humann2/all_genes_annot.fna'
    log:
        log_dir + 'cat_genes_nuc/all.log'
    benchmark:
        benchmark_dir + 'cat_genes_nuc/all.txt'
    run:
        with open(output[0], 'w') as outF:
            for F in input:
                with open(F, 'r') as inF:
                    for line in inF:
                        outF.write(line)

localrules: cat_genes_AA

rule cat_genes_AA:
    """
    Combining genes
    """
    input:
        expand(config['tmp_dir'] + 'vsearch/{sample}_annot_reps.faa',
	       sample = config['samples_unique'])
    output:
        config['tmp_dir'] + 'humann2/all_genes_annot.faa'
    log:
        log_dir + 'cat_genes_AA/all.log'
    benchmark:
        benchmark_dir + 'cat_genes_AA/all.txt'
    run:
        with open(output[0], 'w') as outF:
            for F in input:
                with open(F, 'r') as inF:
                    for line in inF:
                        outF.write(line)

rule compress_genes_nuc:
    """
    Compressing fasta file
    """
    input:
        config['tmp_dir'] + 'humann2/all_genes_annot.fna'
    output:
        humann2_dir + 'all_genes_annot.fna.gz'
    threads:
        4
    conda:
        '../envs/humann2.yaml'
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 24,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 3
    log:
        log_dir + 'compress_genes_nuc/all.log'
    benchmark:
        benchmark_dir + 'compress_genes_nuc/all.txt'
    shell:
        """
        pigz -p {threads} -c {input} > {output} 2> {log}
        """
    
rule compress_genes_AA:
    """
    Compressing fasta file
    """
    input:
        config['tmp_dir'] + 'humann2/all_genes_annot.faa'
    output:
        humann2_dir + 'all_genes_annot.faa.gz'
    threads:
        4
    conda:
        '../envs/humann2.yaml'
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 24,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 3
    log:
        log_dir + 'compress_genes_AA/all.log'
    benchmark:
        benchmark_dir + 'compress_genes_AA/all.txt'
    shell:
        """
        pigz -p {threads} -c {input} > {output} 2> {log}
        """
    
rule humann2_bowtie2_build:
    """
    Running bowtie2 build on combined, annotated genes 
    """
    input:
        humann2_dir + 'all_genes_annot.fna.gz'
    output:
        touch(humann2_dir + 'bowtie2_build.done')
    conda:
        '../envs/humann2.yaml'
    threads:
        24
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 24,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 2 * 3 + 6
    log:
        log_dir + 'bowtie2_build/all.log'
    benchmark:
        log_dir + 'bowtie2_build/all.txt'
    shell:
        """
        PREF=`echo {input} | perl -pe 's/\.[^.]+$//'`

        bowtie2-build --threads {threads} \
          {input} $PREF 2> {log} 1>&2        

        # check that output exists
        INDIR=`dirname {input}`
        IDX_FILES=`find $INDIR -maxdepth 1 -name "*.bt2*"`
        IDX_FILES=`echo $IDX_FILES | perl -pe 's/ +/\n/g' | wc -l`
        if [ $IDX_FILES -lt 1 ]; then
          echo "ERROR: no bowtie2 index files found!"
          exit 1
        fi
        """    

rule humann2_diamond_makedb:
    """
    Running diamond makedb on combined, annotated genes 
    """
    input:
        humann2_dir + 'all_genes_annot.faa.gz'
    output:
        touch(humann2_dir + 'diamond_makedb.done')
    params:
        prefix = humann2_dir + 'all_genes'        
    conda:
        '../envs/humann2.yaml'
    resources:
        time = lambda wildcards, attempt: attempt * 2 * 60 * 24,
        mem_gb_pt = lambda wildcards, attempt: attempt ** 3 * 12
    log:
        log_dir + 'diamond_makedb/all.log'
    benchmark:
        log_dir + 'diamond_makedb/all.txt'
    shell:
        """
        diamond makedb --in {input} -d {params.prefix} 2>&1 > {log}
        """    


